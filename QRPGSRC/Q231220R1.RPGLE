**FREE
//   H**********************************************************************
//   H*                                                                    *
//   H*   システム名      :ＲＰＧ教育                                *
//   H*   サブシステム名  :教育プログラム　　　　　　　　　　　      *
//   H*   プログラム名    :ＳＮＤ－ＭＳＧ                            *
//   H*   プログラムＩＤ  : Q231220R1                                  *
//   H*   会　社　名　　  :株式会社中部システム                      *
//   H*                                                                    *
//   H*     作　成　者    :㈱中部システム Y.USHIDA                   *
//   H*     作　成　日    : 2023/12/07                                 *
//   H*     管　理　番　号: ____________                               *
//   H*                                                                    *
//   H*     変　更　者    :　　　　　　　　　　　　　　　　　　      *
//   H*     変　更　日    : ____/__/__                                 *
//   H*     管　理　番　号:                                            *
//   H*                                                                    *
//   H*  プログラム特記事項                                              *
//   H* 　　　　　　　　　　　＿　　　　　　　　　　　　　　　　　　   *
//   H* 　　　　　　　　　　　＿　　　　　　　　　　　　　　　　　　   *
//   H*                                                                    *
//   H*-*******************************************************************
//   H*-*Ｈ仕様書                                                      **
//   H*-*******************************************************************
CTL-OPT
       DATEDIT(*YMD)
       DECEDIT('0.')
       COPYRIGHT('...')
;
//   D*-********************************************************************
//   D*-* 変数定義                                                      **
//   D*-********************************************************************
DCL-S  WTIMESTAMP    TIMESTAMP                ;  //タイムスタンプ
DCL-S  WDATE8        ZONED(8:0)               ;  //日付
DCL-S  WTIME6        ZONED(6:0)               ;  //時刻

DCL-S  W#CNT1        PACKED(3:0)  INZ         ;  //カウンタ
DCL-S  W#MID         CHAR(007)                ;  //メッセージＩＤ
DCL-S  W#MSG1        CHAR(512)                ;  //メッセージ
DCL-S  W#MSG2        CHAR(048)                ;  //メッセージ
//   C*-********************************************************************
//   C*-* メインルーチン                                                **
//   C*-********************************************************************

//初期処理
EXSR      @INZ                         ;

//情報メッセージ（＊ＩＮＦＯ
W#CNT1 += 1                            ;
W#MSG1  = '(' + %EDITC(W#CNT1:'X') + ')'
        + 'メッセージ＊ＩＮＦＯ（暗黙的'
;
SND-MSG W#MSG1                         ;


//情報メッセージ（＊ＩＮＦＯ※明示的
W#CNT1 += 1                            ;
W#MSG1  = '(' + %EDITC(W#CNT1:'X') + ')'
        + 'メッセージ＊ＩＮＦＯ（明示的'
;
SND-MSG *INFO W#MSG1                   ;


//----------------------------------------------------------
//エスケープ・メッセージ（＊ＥＳＣＡＰＥ
//----------------------------------------------------------

//％ＴＡＲＧＥＴ…メッセージ送信先
W#CNT1 += 1                            ;
W#MSG1  = '(' + %EDITC(W#CNT1:'X') + ')'
        + 'メッセージ＊ＥＳＣＡＰＥ（%TARGET'
;
MONITOR                                ;
  SND-MSG *ESCAPE W#MSG1 %TARGET(*SELF)    ;
ON-ERROR 9999                          ;
  W#MSG2 = W#MSG1                      ;
  DSPLY W#MSG2                         ;
ENDMON                                 ;


//％ＭＳＧ…ＭＳＧＦの取得
W#CNT1 += 1                            ;
W#MSG1  = '(' + %EDITC(W#CNT1:'X') + ')'
        + 'メッセージ＊ＥＳＣＡＰＥ（%MSG'
;
MONITOR                                ;
  W#MID = 'CPF9897'                      ;
  //W#MID = 'XXXXXXX'                      ;
  SND-MSG *ESCAPE
          %MSG(W#MID     : '*LIBL/QCPFMSG' :W#MSG1 )
          %TARGET(*SELF)    ;
ON-ERROR  126                          ;
  W#MSG2 = 'そんなＭＳＧＩＤはないぜよ'  ;
  DSPLY W#MSG2                         ;
ON-ERROR 9999                          ;
  W#MSG2 = W#MSG1                      ;
  DSPLY W#MSG2                         ;
ENDMON                                 ;

//直前の呼び出し元へメッセージ送信
W#CNT1 += 1                            ;
W#MSG1  = '(' + %EDITC(W#CNT1:'X') + ')'
        + 'メッセージ＊ＥＳＣＡＰＥ（直前'
;
SND-MSG *ESCAPE W#MSG1                   ;

//終了処理
EXSR      @END                         ;

//   C*-***************************************************************
//   C*-* @INZ        初期処理                                     **
//   C*-***************************************************************
BEGSR @INZ                                  ;

//システム日付
     WTIMESTAMP = %TIMESTAMP()                       ;
     WDATE8     = %DEC(%DATE(WTIMESTAMP) : *ISO )    ;
     WTIME6     = %DEC(%TIME(WTIMESTAMP) : *HMS )    ;


ENDSR                                       ;
//   C*-***************************************************************
//   C*-* @END        終了処理                                     **
//   C*-***************************************************************
BEGSR @END                                  ;

     *INLR   = *ON                                   ;
     RETURN                                          ;

ENDSR                                       ;
